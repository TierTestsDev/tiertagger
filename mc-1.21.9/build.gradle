plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
    id 'io.freefair.lombok' version '8.12.2'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = "tiertagger-1.21.9-1.21.11"
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    compileOnly "org.projectlombok:lombok:1.18.38"
    annotationProcessor "org.projectlombok:lombok:1.18.38"

    include(implementation("org.yaml:snakeyaml:2.2"))
    implementation "com.google.code.gson:gson:2.10.1"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version

    filesMatching("fabric.mod.json") {
        expand "version": project.version, "minecraft_version": project.minecraft_version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.register("collectJars") {
    dependsOn tasks.named("build")
    doLast {
        def outDir = file("${rootDir}/../out")
        outDir.mkdirs()
        outDir.listFiles()?.findAll { it.name.contains("1.21.9-1.21.11") }?.each { it.delete() }
        fileTree("${buildDir}/libs").matching {
            include "*-${mod_version}.jar"
            exclude "*-dev*"
            exclude "*-sources*"
        }.each { jar ->
            def dest = new File(outDir, "tiertagger-1.21.9-1.21.11-${mod_version}.jar")
            jar.withInputStream { is -> dest.bytes = is.bytes }
        }
    }
}
